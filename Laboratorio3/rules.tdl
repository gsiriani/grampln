; Las palabras que no requieren complementos son elevadas a frases
head-complement-rule-0 := phrase & 
[ HEAD #0,
  SPR #a,
  COMPS < >,   
  ARGS < word & 
         [ HEAD #0,
           SPR #a, 
           COMPS < >, 
           LOC < > ] >  ].

; Las palabras y frases que requieren y presentan un complemento son elevadas a frases
; Se requiere que su complemento se encuentre totalmente complementado
head-complement-rule-1 := phrase &
[ HEAD #0,
  SPR #a, 
  COMPS < >,   
  ARGS < sign & 
         [ HEAD #0,
           SPR #a, 
           COMPS < #1 & [ SPR < > ] > ],  #1 & [ COMPS < > ] >  ].

; Las palabras y frases que requieren dos complementos y presentan uno, lo consumen
; El complemento a consumir debe encontrarse totalmente especificado
head-complement-rule-2 := phrase &
[ HEAD #0,
  SPR #a, 
  COMPS < #2 >,   

  ARGS < sign & 
         [ HEAD #0,
           SPR #a, 
           COMPS < #1, #2 & [ SPR < > ] > ],  #1 >  ].

; Agrupa las frases con un especificador si hay concordancia en numero, genero y persona
head-specifier-rule := phrase &
[ HEAD #0,
  SPR < >,
  COMPS <>,
  ARGS < phrase & 
         #1 & [HEAD [AGR [NUM #num, GEN #gen, PER #per]]] & [ SPR < > ],   phrase & 
                             [ HEAD #0 & [AGR [NUM #num, GEN #gen, PER #per]],
                               SPR < #1 >,
                               COMPS <> ] > ].

; Asocia los modificadores si coinciden en numero y genero
head-modifier-rule-agr := phrase &
[ HEAD #head,
  SPR < #spr >,
  COMPS #comps,
  ARGS < sign & #hdtr &
         [ SPR < #spr >,
           COMPS #comps,
	   HEAD #head & [ AGR [NUM #n, GEN #g] ] ],
	word & 
         [HEAD [MOD < #hdtr > ]] & [HEAD [ AGR [ NUM #n, GEN #g ] ] ]
	> ].

; Asocia los modificadores de las locuciones adverbiales
head-modifier-rule-adv := phrase &
[ HEAD #head,
  SPR < #spr >,
  COMPS #comps,
  ARGS < sign & #hdtr &
         [ SPR < #spr >,
           COMPS #comps,
	   HEAD #head ],
         loc_adv & 
         [HEAD [MOD < #hdtr > ]] > ].

; Crea una locucion adverbial a partir de un adverbio
loc-adv-rule-0 := loc_adv &
[ SPR < >,
  COMPS < >,
  ARGS < word_adv >
].

; Crea una locucion adverbial adjuntando un adverbio a una estructura especificada
; por el rasgo LOC
loc-adv-rule-1 := loc_adv &
[ SPR < >,
  COMPS < >,
  ARGS < word & #x,
         word_adv & [ LOC < #x > ] >
 ].
